// syntax: http://www.lysator.liu.se/c/ANSI-C-grammar-y.html
// {...} can be 0 or more times;  [...] is optional;  x|y alternative

// comments are inline and multi-line C comments /*...*/ and end-of-line
// C++ comments //... Also #... is treated as a comment, to ignore #include etc

// String literals can't contain \0 inside. That is they can, but they end at it.
// Strings and chars can have \r \n \b. No other escapes.

// Global var declarations are understood as var definitions. int a; === int a=0;
// No multiple var declarations (fn declarations are ok).

primary : @Number | @Char | @String | @Id | '(' expr ')'

exprs : expr { ',' expr } // args in calls and the last item in for-header

call_or_index : { '[' expr ']' | '(' [ exprs ] ')' }

postfix : primary call_or_index          // no: postfix ++|--

unexpr : '+''+' unexpr | '-''-' unexpr
       | '-' term | '~' term | '!' term | '*' term  // no: &x
       | postfix

type : "int" | "char" // void is used only for function "result"

stars : [ '*' [ '*' ] ]

term : '(' type stars ')' term
     | '(' expr ')' call_or_index        // shortcut, since we know it's '('
     | unexpr

binop : '*' | '/' | '%'                  // prec. 12
      | '+' | '-'                        // prec. 11    10: << >>
      | '<' | '>' | '<''=' | '>''='      // prec. 9
      | '=''=' | '!''='                  // prec. 8
      | '&' | '^' | '|'                  // prec. 7 6 5
      | '&''&'                           // prec. 4
      | '|''|'                           // prec. 3     2: ?:
      | '='                              // prec. 1     also: += -= <<= ...

expr : term { binop term }

integer : [ '-' ] ( @Number | @Char | @Id )   // enum id is like a number

str_or_0 : @String | 0                        // can be null pointer, see below

arrayinit : @String                           // char a[] = "abc"; // ARRAY!
          | '{' str_or_0 { ',' str_or_0 } '}' // char* b[] = {"ijk","mn",0};
          | '{' integer { ',' integer } '}'   // int c[] = {1,2,3};

vartail : [ '=' integer | '=' @String ]       // for global vars
        | [ '=' expr ]                        // for local vars - any expr.
        | '[' ']' '=' arrayinit               // int** c[] = {0,0};
        | '[' integer ']' [ '=' arrayinit ]   // int d[100] = {0};

morevars : vartail { ',' stars @Id vartail } ';'

vardef_or_expr : type stars @Id morevars      // ends with ';'
               | expr ';'                     // going to change to exprs

stmt : ';'
     | '{' { stmt } '}'
     | "break" ';'                | "return" [ expr ] ';'   // maybe exprs here
     | "while" '(' expr ')' stmt  | "if" '(' expr ')' stmt [ "else" stmt ]
     | "for" '(' vardef_or_expr [ expr ] ';' [ exprs ] ')' stmt
     | vardef_or_expr // ends with ';' maybe expr will be exprs

argdef : type stars @Id        // e.g. "char** argv" but not "char* argv[]"

args : argdef { ',' argdef }

func : '(' [ args ] ')' ';'               // fn declaration
     | '(' [ args ] ')' '{' { stmt } '}'  // fn definition

enumerator : @Id [ '=' integer ]

enumdef : '{' enumerator { ',' enumerator } '}' ';'

decl_or_def : "enum" enumdef              // enums are unnamed
            | "void" @Id func             // function-procedure
            | type stars @Id func         // if next (in func) goes '('
            | type stars @Id morevars     // otherwise

program : decl_or_def { decl_or_def } @Eof  // definition/declaration


# vim: set syntax=ANTLR :
